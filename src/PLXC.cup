import java_cup.runtime.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Stack;
import java.util.Vector;
import java.util.Set;
import java.util.HashSet;

action code {:
    public void checkVariable(Objeto obj) {
          if (obj == null)
              Objeto.errorYPara("[ERROR]\tLa variable no ha sido declarada", new Vector<>(List.of(obj)));

          if (!(obj instanceof Instancia))
              Objeto.errorYPara("[ERROR]\tLa variable no es una instancia", new Vector<>(List.of(obj)));
    }

    public void checkVariable(String id) {
        checkVariable(PLXC.tablaSimbolos.getObj(id));
    }

    public void checkType(Objeto obj, Tipo tipo) {
        if (!(obj instanceof Instancia))
            Objeto.errorYPara("[ERROR]\tNo se puede hacer una asignación si el valor a asignar no es una instancia", new Vector<>(List.of(obj, tipo)));
        Instancia aux = (Instancia) obj;
        if (!aux.getTipoInstancia().isParseable(tipo) )// || aux.getTipoInstancia().getClass() != tipo.getClass()
            Objeto.errorYPara("[ERROR]\tNo se puede hacer una asignación si el valor a asignar no es del mismo tipo o no son parseables entre sii", new Vector<>(List.of(obj, tipo)));
    }

    public Instancia checkElem(Objeto valor) {
        if (!(valor instanceof Instancia))
            Objeto.errorYPara("[ERROR]\tNo se puede formar una coleccion de elementos sin instancias de un mismo tipo mismo tipo", new Vector<>(List.of(valor)));

       return (Instancia) valor;
    }

    private String end_and = "", end_or = "";
    private Stack<String> if_tags = new Stack<>();
    private Stack<SwichOperator> switch_operators = new Stack<>();
    private Stack<Funcion> Funciones = new Stack<>();
    private Objeto res_bool;
:}

parser code {:
    // Redefinition of error functions
    public void report_error(String message, Object info) {
        System.err.print("ERROR: Syntax error");
        if (info instanceof Symbol)
            if (((Symbol)info).left != -1){
                int line = (((Symbol)info).left);
                int column = (((Symbol)info).right);
                Symbol value = (Symbol)info;
                System.err.print(" (linea "+line+", colonna "+column+", símbolo " + sym.terminalNames[value.sym] + "): " + value.toString());
            } else System.err.print(": ");
        else System.err.print(": ");
    }
:}

terminal String ALL, CLL, AP, CP, AC, CC, PYC, COMA, PUNTO, DP, SET;
terminal Tipo INT, FLOAT, CHAR, STRING, U_MINUS, VOID, BOOL;
terminal String ID;
terminal Integer NUM_ENTERO;
terminal Float NUM_REAL;
terminal Integer CARACTER;
terminal String CADENA;
terminal Boolean FALSE, TRUE;
terminal String MAS, MENOS, POR, DIV, EQ, NEQ, LT, GT, LE, GE, AND, OR, NOT, ASIGNA,IMPLICA;
terminal String UNION, ADD, REM;
terminal String IF, WHILE, FOR, FORALL, DO, PRINT, LENGTH, FROM, TO, STEP;
terminal String ELSE, ELSEIF;
terminal String CASE, SWITCH, DEFAULT, BREAK, RETURN;

nonterminal program, lista_sent, sent, sent_comp, sent_simpl, sent_if, sent_while, op_else , sent_do_while, sent_for, for_expr, sent_foreach, sent_switch, list_case, op_default, case_oper, list_sent_simpl, declr_func, impl_func,impl_procedure;
nonterminal Instancia sent_forall;
nonterminal Objeto forall_expr;
nonterminal Set<Instancia> list_param, params;
nonterminal Instancia param;
nonterminal Funcion func_id;
nonterminal String and_end, or_end, else_if;
nonterminal Objeto cond, Get_BoolInstance;
nonterminal List<Objeto> for_all_params;
nonterminal Integer Init_Bloque;
nonterminal List<String> list_id;
nonterminal List<Integer> list_int, list_char;
nonterminal List<Instancia> list_float;
nonterminal List<Instancia> list_valor, set_value;
nonterminal Tipo tipo, declr, declr_conj, tipo_primit;
nonterminal Objeto asig, expr, expr_array, array_length, expr_arit, valor, inic;

precedence  left        IF, ELSE, DO, WHILE, FOR, PRINT;
precedence  right       ASIGNA;
precedence  left        MAS, MENOS;
precedence  left        POR, DIV;
precedence  left        EQ, LE, GE;
precedence  left        NEQ, LT, GT;
precedence  right       IMPLICA;
precedence  right       FORALL;
precedence  left        OR;
precedence  left        AND;
precedence  left        NOT;
precedence  left        ID, NUM_ENTERO, NUM_REAL;
precedence  left        FALSE, TRUE, CARACTER, CADENA;
precedence  left        ELSEIF;
precedence  nonassoc    U_MINUS;
precedence  nonassoc    AP, CP;
precedence  nonassoc    AC, CC;
precedence  nonassoc    ALL, CLL;


start with program;

program ::= lista_sent;

lista_sent ::= sent
            | lista_sent sent
            ;

sent ::= sent_comp
        | sent_simpl
        | declr_func
        | impl_func
        ;

sent_comp ::= ALL Init_Bloque:bloquePrevio lista_sent CLL {:
                  PLXC.tablaSimbolos.delBlock();
                  TablaSimbolos.bloqueActual = bloquePrevio;
:};

sent_simpl ::= valor PYC
        | declr PYC
        | declr_conj PYC
        | PRINT AP valor:valor CP PYC        {:  if (valor != null) RESULT = valor.metodos("PRINT", new Vector());   :}
        | PRINT AP cond:valor CP PYC        {:  if (valor != null) RESULT = valor.metodos("PRINT", new Vector());   :}
        | sent_if
        | sent_while
        | sent_do_while
        | sent_for
        | sent_switch
        | sent_foreach
        ;

list_sent_simpl ::= list_sent_simpl sent_simpl
                 |
                 ;

sent_while ::= WHILE:whileTag
                    // WHILE
                    {:
                    PLXC.out.println(whileTag + ":");
                    :}
                    AP cond:c CP
                    // if c == 0 goto end_while ;
                    {:
                    PLXC.out.println("if (" + c.getNombre() + " == 0) goto end_" + whileTag + ";");
                    :}
                    sent
                    // goto WHILE ;
                    // end_while:
                    {:
                    PLXC.out.println("goto " + whileTag + ";");
                       PLXC.out.println("end_" + whileTag + " :");:}
                    ;

sent_do_while ::= DO:doTag // -> Inicio y final del bucle
                    //DOWHILE:
                    {:
                    PLXC.out.println("DO_" + doTag + ":");
                    :}
                     sent
                    WHILE:whileTag AP cond:c CP PYC// -> Inicio de la condicion de parada
                    {:
                        PLXC.out.println("if (" + c.getNombre() + " == 1) goto DO_" + doTag + " ;");
                        PLXC.out.println("END_DO_" + doTag + ":");
                    :}
                    // END_DOWHILE
                    ;

for_expr ::= valor:expr
            |
            ;

sent_for ::= FOR:forTag AP for_expr:inic
            // init...
            // FOR:
            {:
                PLXC.out.println(forTag + ":");
            :}
            PYC expr:c
            // if (c == 0) goto FOR_END ;
            // goto FOR_BODY ;
            // FOR_ACT:
            {:
                PLXC.out.println("if (" + c.getNombre() + " == 0) goto end_" + forTag + ";");
                PLXC.out.println("goto body_" + forTag + ";");
                PLXC.out.println("act_" + forTag + ":");
            :}
            PYC for_expr:act
            // goto FOR ;
            {:
                PLXC.out.println("goto " + forTag + ";");
            :}
            CP
            // FOR_BODY :
            {:
                PLXC.out.println("body_" + forTag + ":");
            :}
            sent
            {:
                PLXC.out.println("goto act_" + forTag + ";");
                PLXC.out.println("end_" + forTag + ":");
            :}
            // goto FOR_ACT ;
            // FOR_END:
            ;

sent_if ::= IF:ifTag AP expr:c CP       {:
    this.if_tags.push(ifTag);
    PLXC.out.println("if (" + c.getNombre() + " == 0) goto else_" + ifTag + " ;");
:}
            sent                        {:
            PLXC.out.println("goto end_" + ifTag + " ;");
            PLXC.out.println("else_" + ifTag + ":");
:}
            else_if                     {:
            PLXC.out.println("end_" + ifTag + ":");
            this.if_tags.pop();
:};

else_if ::= ELSEIF:elseIfTag AP expr:c CP           {:
    PLXC.out.println(" if (" + c.getNombre() + " == 0) goto else_" + elseIfTag + ";");
:}
                sent                                {:
                PLXC.out.println("goto end_" + this.if_tags.peek() + ";");
                PLXC.out.println("else_" + elseIfTag + ":");
                :}
                else_if
            | ELSE sent
            |
            ;

sent_switch ::= SWITCH:switchTag AP valor:expr CP ALL
                {:
                    // Checkear que la expresion es una instancia de tipo entero
                    if (!(expr instanceof Instancia) && ((Instancia) expr).getTipoInstancia() != TInt.getTInt())
                        Objeto.errorYPara("[ERROR]\tLa expresion del switch no es una instancia de tipo entero", new Vector<>(List.of(expr)));
                    SwichOperator swOper = new SwichOperator(switchTag, (Instancia) expr);
                    this.switch_operators.push(swOper);

                    PLXC.out.println(swOper.getEtiqSwitch() + ":");
                :}
                list_case
                op_default
                {:
                    SwichOperator swOper = this.switch_operators.peek();
                    PLXC.out.println(swOper.getEndSwitch() + ":");
                    this.switch_operators.pop();
                :}
                CLL
                ;

list_case ::= list_case case_oper
            | case_oper
            ;

case_oper ::= CASE:caseEtq valor:expr {:
                  // Checkear que el valor sea una instancia de tipo entero
                  if (!(expr instanceof Instancia) && ((Instancia) expr).getTipoInstancia() != TInt.getTInt())
                        Objeto.errorYPara("[ERROR]\tLa expresion del switch no es una instancia de tipo entero", new Vector<>(List.of(expr)));

                  SwichOperator swOper = this.switch_operators.peek();
                  // if (switchExpr != expr) goto end_caseEtq ;
                  PLXC.out.println(SwichOperator.genCase(caseEtq) + ":");
                  PLXC.out.println("if (" + swOper.getExpr().getNombre() + " != " + expr.getNombre() + ") goto " + SwichOperator.getEndCase(caseEtq) + ";");
                  swOper.addCase(caseEtq);
:}
            DP sent BREAK PYC {:
                    //Salto incondicional al final del switch
                    SwichOperator swOper = this.switch_operators.peek();
                    PLXC.out.println("goto " + swOper.getEndSwitch() + ";");
                    // Etiqueta de fin del end_caseEtq
                    PLXC.out.println(SwichOperator.getEndCase(caseEtq)+ ":");
:}
            ;

op_default ::= DEFAULT DP {:
            SwichOperator swOper = this.switch_operators.peek();
            PLXC.out.println("default_" + swOper.getEtiqSwitch() + ":");
:}
                sent BREAK PYC
            ;

sent_foreach ::= FOR:forTag AP tipo:tipo ID:id {:
                // Añadir la variable itradora a la tablade símbolos
                Instancia iter = new Instancia(id, tipo, TablaSimbolos.bloqueActual, true);
                PLXC.tablaSimbolos.putObj(iter);
:}
                DP expr:expr CP {:
                // Checkear si la expresion es iterable
                if (!(expr instanceof Instancia) || !((Instancia)expr).getTipoInstancia().isIterable())
                    Objeto.errorYPara("[ERROR]\tLa expresion del búcle for each no es iterable.", new Vector<>(List.of(expr)));

                if (expr instanceof SetInstance) {
                    SetInstance set = (SetInstance) expr;
                    Instancia iter = new Instancia(set.getTipoInstancia());

                    set.iterate(iter);
                } else {
                    // En caso de serlo, declarar la etiqueta del búcle y asignar valor a la variable iteradora
                    Instancia index = new Instancia("$_FOREACH_IDX_".concat(forTag), TInt.getTInt(), TablaSimbolos.bloqueActual, true);
                    PLXC.tablaSimbolos.putObj(index);
                    Instancia iter = (Instancia) PLXC.tablaSimbolos.getObj(id, TablaSimbolos.bloqueActual);
                    Instancia colec = (Instancia) expr;

                    PLXC.out.println(index.getNombre() + " = 0;");
                    PLXC.out.println("foreach_" + forTag + ":");

                    if (colec instanceof StringInstancia) {
                        if (iter.getTipoInstancia() != TChar.getTChar())
                            Objeto.errorYPara("[ERROR]\tLa variable iteradora del búcle for each necesita ser de tipo char.", new Vector<>(List.of(iter)));

                        StringInstancia.iterate(index, (StringInstancia) colec, iter, "end_foreach_".concat(forTag));
                    } else if (colec.getTipoInstancia() == TString.getInstance()){
                        Objeto.errorYPara("[ERROR]\tLa expresion del búcle for each necesita un StringInstance no una instancia de tipo String.", new Vector<>(List.of(expr)));
                    } else if (colec.getTipoInstancia() instanceof TArray) {
                        TArray.iterate(index, (TArray) colec.getTipoInstancia(), colec, iter, "end_foreach_".concat(forTag));
                    }


                    // Instanciar la variable índice con la que se hará en control y acceso a los elementos
                    // (Sentencia de control, la colección ha de contener el número de iteraciones)
                }

                :}
                sent
                {:
                // Resetear el iterador
                Instancia idx = (Instancia) PLXC.tablaSimbolos.getObj("$_FOREACH_IDX_".concat(forTag), TablaSimbolos.bloqueActual);

                idx.metodos("INCREMENTA_1", new Vector<>());
                PLXC.out.println("goto foreach_" + forTag + ";");
                PLXC.out.println("end_foreach_" + forTag + ":");
:}
               ;

declr ::= tipo:tipo ID:id  inic:inic                          {:
        if (inic != null) {
            Instancia newVar;

            if (tipo != TString.getInstance())
                 newVar = new Instancia(id, tipo, TablaSimbolos.bloqueActual, true);
            else
                 newVar = new StringInstancia(id);

            PLXC.tablaSimbolos.putObj(newVar);
            newVar.metodos("ASIGNA",new Vector<>(List.of(inic)));
        } else {
            Instancia newVar = new Instancia(id, tipo, TablaSimbolos.bloqueActual, true);
            PLXC.tablaSimbolos.putObj(newVar);
        }

        RESULT = tipo;
:}
        | tipo:tipo ID:id AC expr:tam CC inic:inic            {:

        if (inic != null) {
            Instancia newVar = new Instancia(id, new TArray(tipo, tam), TablaSimbolos.bloqueActual, true);
            PLXC.tablaSimbolos.putObj(newVar);

            newVar.metodos("ASIGNA",new Vector<>(List.of(inic)));
        } else {
            Instancia newVar = new Instancia(id, new TArray(tipo, tam), TablaSimbolos.bloqueActual, true);
            PLXC.tablaSimbolos.putObj(newVar);
        }


        RESULT = tipo;
        :}
        | declr:tipo COMA ID:id  inic:inic                    {:
                                  if (inic != null) {
                                      Instancia newVar;

                                      if (tipo != TString.getInstance())
                                           newVar = new Instancia(id, tipo, TablaSimbolos.bloqueActual, true);
                                      else
                                           newVar = new StringInstancia(id);

                                      PLXC.tablaSimbolos.putObj(newVar);
                                      newVar.metodos("ASIGNA",new Vector<>(List.of(inic)));
                                  } else {
                                      Instancia newVar = new Instancia(id, tipo, TablaSimbolos.bloqueActual, true);
                                      PLXC.tablaSimbolos.putObj(newVar);
                                  }

                                  RESULT = tipo;
                          :}
        | declr:tipo COMA ID:id AC expr:tam CC inic:inic      {:

                                  if (inic != null) {
                                      Instancia newVar = new Instancia(id, new TArray(tipo, tam), TablaSimbolos.bloqueActual, true);
                                      PLXC.tablaSimbolos.putObj(newVar);

                                      newVar.metodos("ASIGNA",new Vector<>(List.of(inic)));
                                  } else {
                                      Instancia newVar = new Instancia(id, new TArray(tipo, tam), TablaSimbolos.bloqueActual, true);
                                      PLXC.tablaSimbolos.putObj(newVar);
                                  }


                                  RESULT = tipo;
                                  :}
        ;

inic ::= ASIGNA expr:exp        {:RESULT = exp;:}
        |
        ;

set_value ::= ALL list_valor:inic CLL {: RESULT = inic;:}
            | ALL CLL                 {: RESULT = new ArrayList<Instancia>();:}
            | ID:id {:
            Objeto expr = PLXC.tablaSimbolos.getObj(id, TablaSimbolos.bloqueActual);
            if (!(expr instanceof SetInstance)) {
                Objeto.errorYPara("[ERROR]\tLa expresion proporcionada como set value no es una instancia de SetInstace", new Vector<>(List.of(expr)));
            } else {
                SetInstance setValue = (SetInstance) expr;
                RESULT = setValue.getElems();
            }

:}
            ;

declr_conj ::= SET tipo:tipo ID:id ASIGNA set_value:inic {:
        Instancia newVariable = new SetInstance(id, tipo, inic, TablaSimbolos.bloqueActual, true);

        PLXC.tablaSimbolos.putObj(newVariable);
:}
            | SET tipo:tipo ID:id {:
            SetInstance newVar = new SetInstance(id, tipo, TablaSimbolos.bloqueActual, true);
            PLXC.tablaSimbolos.putObj(newVar);
:}
            ;

list_id ::=  ID:id                          {:
            List<String> start = new ArrayList<>();
            start.add(id);
            RESULT = start; :}
         | list_id:list COMA ID:id          {:  list.add(id); RESULT = list;        :};

list_valor ::= list_valor:l COMA valor:val      {:
            Instancia elem = checkElem(val);

            if (l.get(0).getTipoInstancia() != elem.getTipoInstancia())
                Objeto.errorYPara("[ERROR]\tEl elemento de la colección no es del mismo tipo que el resto", new Vector<>());

            l.add(elem);
            RESULT = l;
:}
            | valor:val                {:
            Instancia elem = checkElem(val);
            List<Instancia> start = new ArrayList<>();

            start.add(elem);

            RESULT = start;
:}
            ;

expr ::= valor:expr                 {:  RESULT = expr;  :}
        | expr_array:expr           {:
        RESULT = expr;
        :}
        | cond:cond                 {:  RESULT = cond;       :}
        ;


valor ::= ID:id PUNTO LENGTH                {:
            checkVariable(id);
            Objeto a = PLXC.tablaSimbolos.getObj(id);

          if (!(a instanceof Instancia) && (((Instancia) a).getTipoInstancia() instanceof TArray))
              Objeto.errorYPara("[ERROR]\tNo se puede hacer una asignación si el valor a asignar no es una instancia", new Vector<>(List.of(a)));


          RESULT = a.metodos(TArray.ARRAY_METHODS.LENGTH.name(), new Vector<>());
      :}
      | func_id:f AP list_valor:params CP             {:
        f.callFunction(new ArrayList(params));

        RESULT = f.getReturnInstancia();
:}
      | ID:id AC valor:idx CC ASIGNA valor:valor      {:
          // Checkear que el valor es una instancia y no es de tipo array
          checkVariable(id);
          // Checkear que le valor es del mismo que la variable
          Instancia variable = (Instancia) PLXC.tablaSimbolos.getObj(id);

          if (!(variable.getTipoInstancia() instanceof TArray))
              Objeto.errorYPara("[ERROR]\tLa variable no es un array", new Vector<>(List.of(variable)));

          RESULT = variable.metodos("SET", new Vector<>(List.of(idx, valor)));
      :}
      | ID:id ASIGNA expr:valor             {:
                this.checkVariable(id); // Comprueba que la variable este declarada y que sea una instancia
                Instancia variable = (Instancia) PLXC.tablaSimbolos.getObj(id);

                if (!(variable.getTipoInstancia() instanceof TArray)) {
                      this.checkType(valor, variable.getTipoInstancia()); // Comprueba que el tipo de la variable sea el mismo que el de la expresion o sean parseables entre sii
                }

                variable.metodos("ASIGNA", new Vector<>(List.of(valor)));

                RESULT = valor;
            :}
      | AP tipo:tipoDst CP valor:exp        {:

      if (exp instanceof Instancia) {
          Instancia aux = ((Instancia)exp).getTipoInstancia().cast(tipoDst, (Instancia) exp);
          if (aux != null) {
              RESULT = aux;
          } else {
              Objeto.errorYPara("[ERROR]\tNo se puede hacer una asignación si el valor a asignar no es del mismo tipo o no son parseables entre sii", new Vector<>());
          }
      } else {
          Objeto.errorYPara("[ERROR]\tNo se puede hacer un casting si el valor a asignar no es una instancia", new Vector<>());
      }

      :}
      | ID:id                     {:  checkVariable(id); RESULT = PLXC.tablaSimbolos.getObj(id);  :}
      | ID:id AC valor:idx CC     {:
          checkVariable(id);
          Instancia variable = (Instancia) PLXC.tablaSimbolos.getObj(id);

          if (!(variable instanceof Instancia) && (((Instancia) variable).getTipoInstancia() instanceof TArray))
              Objeto.errorYPara("[ERROR]\tLa variable no es un array", new Vector<>(List.of(variable)));

          RESULT = variable.metodos("GET", new Vector<>(List.of(idx)));

      :}
      | ID:idA UNION ID:idB       {:
            SetInstance a = null, b = null;
            Objeto aux = PLXC.tablaSimbolos.getObj(idA, TablaSimbolos.bloqueActual);
            if (! (aux instanceof SetInstance)) {
                Objeto.errorYPara("[ERROR]\tLa unión de conjuntos necesita des objeto de tipo SetInstance", new Vector<>());
            } else {
                a = (SetInstance) aux;
            }

            aux = PLXC.tablaSimbolos.getObj(idB, TablaSimbolos.bloqueActual);
            if (! (aux instanceof SetInstance)) {
                Objeto.errorYPara("[ERROR]\tLa unión de conjuntos necesita des objeto de tipo SetInstance", new Vector<>());
            } else {
                b = (SetInstance) aux;
            }

            RESULT = a.metodos(TSet.SET_METHODS.UNION.name(), new Vector<>(List.of(b)));
:}
      | ID:id ADD valor:value   {:
       if (!(value instanceof Instancia))
            Objeto.errorYPara("[ERROR]\tNo se pueden añadir elementos a un conjunto si estos no son instancias", new Vector<>(List.of(value)));

       SetInstance set = (SetInstance) PLXC.tablaSimbolos.getObj(id, TablaSimbolos.bloqueActual);
       Instancia elem = (Instancia) value;

       RESULT = set.metodos(TSet.SET_METHODS.ADD.name(), new Vector<>(List.of(elem)));
:}
      | NUM_ENTERO:num            {:
      RESULT = new Instancia(num.toString(), TInt.getTInt(), TablaSimbolos.bloqueActual, false);
      :}
      | NUM_REAL:num              {:
      RESULT = new Instancia(num.toString(), TFloat.getTFloat(), TablaSimbolos.bloqueActual, false);
      :}
      | CARACTER:c                {:
      RESULT = new Instancia(c.toString(), TChar.getTChar(), TablaSimbolos.bloqueActual, false);
      :}
      | CADENA:s                  {:
          char[] chars = s.toCharArray();
          StringInstancia res = new StringInstancia(chars);

          res = TString.asignaConstante(res, chars);

          RESULT = res;
      :}
      | expr_arit:expr_arit       {:  RESULT = expr_arit;  :}
      ;

expr_arit ::= AP valor:exp CP             {:  RESULT = exp;   :}
          | valor:a MAS:op valor:b      {:  RESULT = a.metodos(op, new Vector(List.of(b)));     :}
          | valor:a MENOS:op valor:b    {:  RESULT = a.metodos(op, new Vector(List.of(b)));     :}
          | valor:a POR:op valor:b      {:  RESULT = a.metodos(op, new Vector(List.of(b)));     :}
          | valor:a DIV:op valor:b      {:  RESULT = a.metodos(op, new Vector(List.of(b)));     :}
          | MENOS:op valor:exp         {:  RESULT = exp.metodos("UMENOS", new Vector());       :}   %prec U_MINUS
          ;

expr_array ::= ALL list_valor:l CLL       {:
                Instancia res = new Instancia(new TArray(l.get(0).getTipoInstancia(), String.valueOf(l.size())));
                for (int i = 0; i < l.size(); i++) {
                     PLXC.out.println(res.getNombre() + "[" + i + "] = " + l.get(i).getNombre() + ";");
                }

                RESULT = res;
:};


cond ::=  AP expr:a CP          {:  RESULT = a;     :}
        | NOT expr:a            {:  RESULT = a.metodos("NOT", new Vector());   :}
        | expr:a  AND              {:
        this.end_and = PLXC.tablaSimbolos.getNewEtiq();
        this.res_bool = new Instancia(TBool.getTBool());
        PLXC.out.println(res_bool.getNombre() + " = " + a.getNombre() + ";");
        PLXC.out.println("if (" + a.getNombre() + " == 0) goto " + end_and + ";");
:}
            expr:b          {:
            PLXC.out.println(res_bool.getNombre() + " = " + b.getNombre() + ";");
            PLXC.out.println(this.end_and + ":");
            RESULT = res_bool;
:}
        | expr:a  OR               {:
        this.end_or = PLXC.tablaSimbolos.getNewEtiq();
        this.res_bool = new Instancia(TBool.getTBool());
        PLXC.out.println(res_bool.getNombre() + " = " + a.getNombre() + ";");
        PLXC.out.println("if (" + a.getNombre() + " == 1) goto " + end_or + ";");
:}
            expr:b           {:
            PLXC.out.println(res_bool.getNombre() + " = " + b.getNombre() + ";");
            PLXC.out.println(this.end_or + ":");
            RESULT = res_bool;
:}
        | expr:p IMPLICA expr:q  {:

        if (!(p instanceof Instancia) && !(q instanceof Instancia)) {
            Objeto.errorYPara("[ERROR]\tLa operación implica necesita de instancias", new Vector<>(List.of(p, q)));
        }

        if (((Instancia) p).getTipoInstancia() != TBool.getTBool() || ((Instancia) q).getTipoInstancia() != TBool.getTBool()) {
            Objeto.errorYPara("[ERROR]\tLa operación implica necesita de instancias de tipo booleano", new Vector<>(List.of(p, q)));
        }

        RESULT = p.metodos(TBool.BOOL_METHODS.IMPLICA.name(), new Vector<>(List.of(q)));
:}
        | sent_forall:res        {:  RESULT = res;:}
        | valor:a GT valor:b      {:  RESULT = a.metodos("MAYOR", new Vector(List.of(b)));          :}
        | valor:a LT valor:b      {:  RESULT = a.metodos("MENOR", new Vector(List.of(b)));          :}
        | valor:a GE valor:b      {:  RESULT = a.metodos("MAYOR_IGUAL", new Vector(List.of(b)));    :}
        | valor:a LE valor:b      {:  RESULT = a.metodos("MENOR_IGUAL", new Vector(List.of(b)));    :}
        | valor:a EQ valor:b      {:  RESULT = a.metodos("IGUAL", new Vector(List.of(b)));          :}
        | valor:a NEQ valor:b     {:  RESULT = a.metodos("DISTINTO", new Vector(List.of(b)));       :}
        | FALSE                 {:  RESULT = new Instancia("0", TBool.getTBool(), TablaSimbolos.bloqueActual, false);   :}
        | TRUE                  {:  RESULT = new Instancia("1", TBool.getTBool(), TablaSimbolos.bloqueActual, false);    :}
        ;

sent_forall ::= FORALL:label ID:id {:
                Instancia res = new Instancia("forall_res_" + label, TBool.getTBool(), TablaSimbolos.bloqueActual, false);
                PLXC.tablaSimbolos.putObj(res);

                PLXC.out.println(res.getNombre() + " = 1;");
                PLXC.out.println(id + " = 0;");
                PLXC.out.println("goto cond_forall_" + label + ";");
                PLXC.out.println("incr_forall_" + label + ":");
                PLXC.out.println(id + " = " + id + " + 1;");
                PLXC.out.println("if (" + id + " == " + 2 + ") goto end_forall" + label + ";");
                PLXC.out.println("if (" + 2 + " < " + id + ") goto end_forall" + label + ";");
                PLXC.out.println("cond_forall_" + label + ":");
:}
                COMA expr:condicion {:

                if (!(condicion instanceof Instancia))
                    Objeto.errorYPara("[ERROR]\tLa condicion del forall no es una instancia", new Vector<>(List.of(condicion)));

                Objeto res = PLXC.tablaSimbolos.getObj("forall_res_" + label);
                if (! (res instanceof Instancia))
                    Objeto.errorYPara("[ERROR]\tLa variable de resultado del forall no es una instancia", new Vector<>(List.of(res)));

                PLXC.out.println("if (" + condicion.getNombre() + " == 1) goto incr_forall_" + label + ";");
                PLXC.out.println(res.getNombre() + " = 0;");
                PLXC.out.println("end_forall" + label + ":");

                RESULT = (Instancia) res;

:}
                | FORALL:label ID:id FROM valor:inic TO valor:limit {:

                Instancia res = new Instancia("forall_res_" + label, TBool.getTBool(), TablaSimbolos.bloqueActual, false);
                PLXC.tablaSimbolos.putObj(res);

                PLXC.out.println(res.getNombre() + " = 1;");
                PLXC.out.println(id + " = " + inic.getNombre() +";");
                PLXC.out.println("goto cond_forall_" + label + ";");
                PLXC.out.println("incr_forall_" + label + ":");
                PLXC.out.println(id + " = " + id + " + 1;");
                PLXC.out.println("if (" + id + " == " + limit.getNombre() + ") goto end_forall" + label + ";");
                PLXC.out.println("if (" + limit.getNombre() + " < " + id + ") goto end_forall" + label + ";");
                PLXC.out.println("cond_forall_" + label + ":");
:}
                COMA expr:condicion {:
                if (!(condicion instanceof Instancia))
                    Objeto.errorYPara("[ERROR]\tLa condicion del forall no es una instancia", new Vector<>(List.of(condicion)));

                Objeto res = PLXC.tablaSimbolos.getObj("forall_res_" + label);
                if (! (res instanceof Instancia))
                    Objeto.errorYPara("[ERROR]\tLa variable de resultado del forall no es una instancia", new Vector<>(List.of(res)));

                PLXC.out.println("if (" + condicion.getNombre() + " == 1) goto incr_forall_" + label + ";");
                PLXC.out.println(res.getNombre() + " = 0;");
                PLXC.out.println("end_forall" + label + ":");

                RESULT = (Instancia) res;
                :}
                | FORALL:label ID:id FROM valor:inic TO valor:limit STEP valor:incr {:
                Instancia res = new Instancia("forall_res_" + label, TBool.getTBool(), TablaSimbolos.bloqueActual, false);
                PLXC.tablaSimbolos.putObj(res);

                PLXC.out.println(res.getNombre() + " = " + inic.getNombre() + ";");
                PLXC.out.println(id + " = 0;");
                PLXC.out.println("goto cond_forall_" + label + ";");
                PLXC.out.println("incr_forall_" + label + ":");
                PLXC.out.println(id + " = " + id + " + " + incr.getNombre() + ";");
                PLXC.out.println("if (" + id + " == " + limit.getNombre() + ") goto end_forall" + label + ";");
                PLXC.out.println("if (" + limit.getNombre() + " < " + id + ") goto end_forall" + label + ";");
                PLXC.out.println("cond_forall_" + label + ":");
:}
                COMA expr:condicion {:

                if (!(condicion instanceof Instancia))
                    Objeto.errorYPara("[ERROR]\tLa condicion del forall no es una instancia", new Vector<>(List.of(condicion)));

                Objeto res = PLXC.tablaSimbolos.getObj("forall_res_" + label);
                if (! (res instanceof Instancia))
                    Objeto.errorYPara("[ERROR]\tLa variable de resultado del forall no es una instancia", new Vector<>(List.of(res)));

                PLXC.out.println("if (" + condicion.getNombre() + " == 1) goto incr_forall_" + label + ";");
                PLXC.out.println(res.getNombre() + " = 0;");
                PLXC.out.println("end_forall" + label + ":");

                RESULT = (Instancia) res;
                :}
                ;

for_all_params ::= forall_expr:condicion COMA for_all_params:l {:
        l.add(condicion);
        RESULT = l;
        :}
                |  {:
                List<Objeto> start = new ArrayList<>();

                //start.add(condicion);

                RESULT = start;
:}
                ;

forall_expr ::= expr:expr {: RESULT = expr;:};

tipo ::= tipo_primit:t                  {:
        RESULT = t;
        :}
        | tipo:t ALL array_length:exp CLL       {:

        if (exp != null) {
            RESULT = new TArray(t, exp.getNombre());
        } else {
            RESULT = new TArray(t);
        }

:}
        | tipo:t ALL CLL ALL CLL          {:

        TArray tfila = new TArray(t);
        TArray tmatriz = new TArray(tfila);

        RESULT = tmatriz;

:}
        |                               {:    RESULT = TInt.getTInt();            :}
        ;

tipo_primit ::= INT:t                          {:    RESULT = TInt.getTInt();            :}
            | FLOAT:t                       {:    RESULT = TFloat.getTFloat();        :}
            | CHAR:t                        {:    RESULT = TChar.getTChar();          :}
            | STRING:t                      {:    RESULT = TString.getInstance();     :}
            | VOID:t                        {:    RESULT = TVoid.getTVoid();          :}
            | BOOL:t                        {:    RESULT = TBool.getTBool();:}
            ;

array_length ::= expr:expr   {: RESULT = expr; :}
              |
              ;

Init_Bloque ::=  {:  RESULT = TablaSimbolos.newBloque();     :};

and_end ::= {:  RESULT = PLXC.tablaSimbolos.getNewEtiq();     :};

or_end ::= {:  RESULT = PLXC.tablaSimbolos.getNewEtiq();     :};

Get_BoolInstance ::= {: RESULT = new Instancia(TBool.getTBool());:};

